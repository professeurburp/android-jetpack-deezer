apply plugin: 'com.android.application'
apply plugin: "androidx.navigation.safeargs" // SafeArgs for Arch Components navigation

android {

    compileSdkVersion 28
    buildToolsVersion = '28.0.3'

    defaultConfig {
        applicationId "com.professeurburp.deezerapitest"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Keep track of db schemas
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    dataBinding {
        enabled = true
    }

    buildTypes.each {
        it.buildConfigField 'int', 'USER_ID', USER_ID
        it.buildConfigField 'int', 'FAVORITES_LIMIT', FAVORITES_LIMIT
    }
}

dependencies {

    def glideVersion = '4.9.0'
    def retrofitVersion = '2.5.0'
    def daggerVersion = '2.17'
    def flattenVersion = '0.8.1'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // --- AndroidX ---

    // implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    // Navigation
    implementation "androidx.navigation:navigation-fragment:2.0.0"
    implementation "androidx.navigation:navigation-ui:2.0.0"
    // Room - ORM for data persistence
    implementation 'androidx.room:room-runtime:2.0.0'
    annotationProcessor 'androidx.room:room-compiler:2.0.0'

    // --- Third-party libs ---

    // Glide - image processing
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"
    // Retrofit - REST
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    // Dagger2 - DI framework
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    // Flatten - easy JSON flattening
    implementation "org.itishka.gson-flatten:gson-flatten:$flattenVersion"

    // --- Testing libs ---
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
}
